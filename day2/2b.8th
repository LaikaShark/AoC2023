var sum
true var, valid
1 var, game
0 var, maxR \ 12 max
0 var, maxG \ 13 max
0 var, maxB \ 14 max

: trim-head \ s -- [s-"game: X"] 
    0 7 game @ G:>s s:len nip n:+ s:-
;

\ check each showing in a game
: check-showing 
    s:ltrim 
    "," /                     \ split on each block
    ( s:ltrim                 \ trim leading whitespace
      cr dup . "   \t" .      \ output current check
      \ check if red
      "red" s:count-match     \ returns nonzero if found
      if dup
        -4 s:lsub >n          \ extract number from string
        dup maxR @ n:>            \ less than max number? 
        if
          maxR !
          " new max R" .
        else
          drop " " .
        then
      then
      
      "blue" s:count-match
      if dup
        -5 s:lsub >n
        dup maxB @ n:> 
        if
          maxB !
          " new max B" .
        else
          drop " " .
        then
      then
      
      "green" s:count-match
      if dup
        -6 s:lsub >n
        dup maxG @ n:> 
        if
          maxG !
          " new max G" .
        else
          drop " " .
        then
      then
      drop
    ) a:each
;

\ game xxx: show1; show2; shown

: main 
  "input" f:open 
  (
    trim-head \ remove "game: xxx" from string
    ";" /     \ split line on ; to get each showing
    cr "game: " . game @ . cr
    "sum: " . sum @ . cr
    ( check-showing ) a:each \ check each showing
   
    maxR @ maxG @ maxB @ n:* n:* \ mull all maxes together
    sum @ n:+ sum ! \ add to sum

    0 maxR !
    0 maxG !
    0 maxB !
    1 game @ n:+ game ! \ increment game counter
    cr
  ) f:eachline 
  cr
  f:close
  sum @ . cr
;
main
bye
